SECTIONS {
  .note.gnu.build-id : { *(.note.gnu.build-id) }

  . = 0x80000; /* Raspbery Pi 3 Aarch64 (kernel8.img) load address */

  /* start of the binary */
  __text_beg = .;

  __code_beg = .;

  .text : {
      KEEP(*(.text.init)) /* from init.S */
      *(.text .text.* .gnu.linkonce.t*)
  }

  __code_end = .;

  .rodata : {
    *(.rodata .rodata.* .gnu.linkonce.r*)
  }

  .data : {
    *(.data .data.* .gnu.linkonce.d*)
  }

  .bss (NOLOAD) : {
    . = ALIGN(32);
    __bss_beg = .;
    *(.bss .bss.*)
    *(COMMON)
    . = ALIGN(8);
    __bss_end = .;
  }

  /* end of the binary */
  __text_end = ALIGN(8);

  /* number of bytes in BSS section and complete binary */
  __bss_len = (__bss_end - __bss_beg);
  __text_len = (__text_end - __text_beg);

  /DISCARD/ : { *(.comment) *(.gnu*) *(.note*) *(.eh_frame*) }
}
